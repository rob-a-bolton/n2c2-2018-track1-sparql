PREFIX umls: <https://identifiers.org/umls:>
PREFIX n2c2: <https://n2c2.localhost/terminology#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX snomed: <http://purl.bioontology.org/ontology/SNOMEDCT/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX train: <https://n2c2.localhost/datasets/n2c2-train/>

SELECT ?status (count(distinct ?patient) AS ?total)
FROM <https://n2c2.localhost/datasets/n2c2-train/>
FROM NAMED <http://purl.bioontology.org/ontology/SNOMEDCT/>
WHERE {
  ?patient a snomed:116154003;
           n2c2:n2c2_asp_for_mi ?status.
  {
    GRAPH <http://purl.bioontology.org/ontology/SNOMEDCT/> {
      {
        [ snomed:has_active_ingredient|snomed:has_precise_active_ingredient [ rdfs:subClassOf* snomed:387458008 ];
          umls:has_cui ?asp_cui ].
      }
      UNION
      {
        [ rdfs:subClassOf* snomed:387458008; # Aspirin
          umls:has_cui ?asp_cui ]
      }
    }
    ?patient n2c2:has_document/n2c2:has_annotation/n2c2:has_cui ?asp_cui.
  }
  {
    GRAPH <http://purl.bioontology.org/ontology/SNOMEDCT/> {
      [ rdfs:subClassOf* snomed:251052000; # Myocardial finding
        umls:has_cui ?myo_cui ].
    }
    ?patient n2c2:has_document/n2c2:has_annotation/n2c2:has_cui ?myo_cui;
  }       
} GROUP BY ?status

PREFIX umls: <https://identifiers.org/umls:>
PREFIX n2c2: <https://n2c2.localhost/terminology#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX snomed: <http://purl.bioontology.org/ontology/SNOMEDCT/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX train: <https://n2c2.localhost/datasets/n2c2-train/>

SELECT ?status (count(?patient) AS ?total)
FROM <https://n2c2.localhost/datasets/n2c2-train/>
FROM NAMED <http://purl.bioontology.org/ontology/SNOMEDCT/>
WHERE {
  SELECT DISTINCT ?patient ?status {
    GRAPH <http://purl.bioontology.org/ontology/SNOMEDCT/> {
      { 
        [ rdfs:subClassOf* snomed:435591000124104; # Nutrition supplement therapy
          snomed:has_direct_substance ?_ ]
      }
      UNION
      { ?_ rdfs:subClassOf* snomed:373453009 } # Nutrition supplement
      MINUS 
      { ?_ rdfs:subClassOf* snomed:30178006 } # Vitamin D, this actually -TP/+FN
      ?_ umls:has_cui ?cui.
    }
    ?ann n2c2:has_cui ?cui.
    ?doc n2c2:has_annotation ?ann.
    ?patient n2c2:has_document ?doc;
            a snomed:116154003;
            n2c2:n2c2_dietsupp_2mos ?status.
  }
} GROUP BY ?status
# From
# 2020-06-14
# To
# 2173-02-04

# ?doc n2c2:has_date ?date.
# FILTER (?date > "2172-12-01"^^xsd:date)
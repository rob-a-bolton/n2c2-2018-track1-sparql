PREFIX umls: <https://identifiers.org/umls:>
PREFIX n2c2: <https://n2c2.localhost/terminology#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX snomed: <http://purl.bioontology.org/ontology/SNOMEDCT/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX train: <https://n2c2.localhost/datasets/n2c2-train/>

SELECT ?status (count(?patient) AS ?total)
FROM <https://n2c2.localhost/datasets/n2c2-train/>
FROM NAMED <http://purl.bioontology.org/ontology/SNOMEDCT/>
WHERE {
  SELECT DISTINCT ?patient ?status {
    GRAPH <http://purl.bioontology.org/ontology/SNOMEDCT/> {
      { ?_ rdfs:subClassOf* snomed:43396009 }  # Hemoglobin A1c measurement
      UNION
      { ?_ rdfs:subClassOf* snomed:365614008 } # Hemoglobin variant finding
      UNION
      { ?_ rdfs:subClassOf* snomed:365845005 } # Hemoglobin A1C - diabetic control finding
      UNION
      { ?_ rdfs:subClassOf* snomed:313995005 } # Hemoglobin A measurement
      UNION
      { ?_ rdfs:subClassOf* snomed:31299006 }  # Hemoglobin A
      
      ?_ umls:has_cui ?cui.
    }
    ?patient a snomed:116154003;
             n2c2:has_document/n2c2:has_annotation/n2c2:has_cui ?cui;
             n2c2:n2c2_hba1c ?status.
  }
} GROUP BY ?status